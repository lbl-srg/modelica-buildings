within Buildings.Fluid.ZoneEquipment.FanCoilUnit.Examples;
model SingleZoneVariableFanConstantWaterFlowrate
  "Example model for a system with single-zone serviced by FCU with variable fan, constant flow pump control"

  extends Modelica.Icons.Example;

  extends Buildings.Fluid.ZoneEquipment.BaseClasses1.ExampleTestbed(
    redeclare
      Buildings.Fluid.ZoneEquipment.FanCoilUnit.Controls.VariableFanConstantWaterFlowrate
      conZonHVACSys(
      final controllerTypeCoo=Buildings.Controls.OBC.CDL.Types.SimpleController.PI,

      final kCoo=0.5,
      final TiCoo=300,
      final TdCoo=0.5,
      final controllerTypeHea=Buildings.Controls.OBC.CDL.Types.SimpleController.P,

      final kHea=0.5,
      final minFanSpe=0.15,
      final tFanEnaDel=60,
      final tFanEna=600,
      final dTHys=0.5),
    redeclare Buildings.Fluid.ZoneEquipment.FanCoilUnit.FourPipe_corrected
      zonHVACSys(
      final heaCoiTyp=Buildings.Fluid.ZoneEquipment.BaseClasses1.Types.HeaSou.hotWat,

      oaPorTyp=Buildings.Fluid.ZoneEquipment.BaseClasses1.Types.OAPorts.oaMix,
      final dpAir_nominal(displayUnit="Pa") = 100,
      final mAirOut_flow_nominal=FCUSizing.mAirOut_flow_nominal,
      redeclare package MediumA = MediumA,
      redeclare package MediumCHW = MediumW,
      redeclare package MediumHW = MediumW,
      final mAir_flow_nominal=FCUSizing.mAir_flow_nominal,
      final mHotWat_flow_nominal=FCUSizing.mHotWat_flow_nominal,
      final QHeaCoi_flow_nominal=13866,
      final UAHeaCoi_nominal=FCUSizing.UAHeaCoi_nominal,
      final mChiWat_flow_nominal=FCUSizing.mChiWat_flow_nominal,
      final UACooCoi_nominal=FCUSizing.UACooCoiTot_nominal,
      redeclare Buildings.Fluid.ZoneEquipment.FanCoilUnit.Examples.Data.FanData
        fanPer),
    building(
      final idfName=Modelica.Utilities.Files.loadResource(
          "./Buildings/Resources/Data/Fluid/ZoneEquipment/FanCoilUnit/FanCoilAutoSize_ConstantFlowVariableFan.idf"),

      final epwName=Modelica.Utilities.Files.loadResource(
          "./Buildings/Resources/weatherdata/USA_IL_Chicago-OHare.Intl.AP.725300_TMY3.epw"),

      final weaName=Modelica.Utilities.Files.loadResource(
          "./Buildings/Resources/weatherdata/USA_IL_Chicago-OHare.Intl.AP.725300_TMY3.mos")),

    zon(final zoneName="West Zone", final nPorts=2));

  replaceable package MediumA = Buildings.Media.Air
    constrainedby Modelica.Media.Interfaces.PartialCondensingGases
    "Medium model for air";

  replaceable package MediumW = Buildings.Media.Water
    "Medium model for water";

  Buildings.Fluid.ZoneEquipment.FanCoilUnit.Examples.Data.SizingData FCUSizing
    "Sizing parameters for fan coil unit"
    annotation (Placement(transformation(extent={{-140,-120},{-120,-100}})));

equation

  annotation (Icon(coordinateSystem(preserveAspectRatio=false,
      extent={{-100,-100},{100,100}})),
    Diagram(coordinateSystem(preserveAspectRatio=false,
      extent={{-160,-160},{160,160}})),
    experiment(
      StopTime=86400,
      Interval=60,
      Tolerance=1e-06),
    __Dymola_Commands(file= "modelica://Buildings/Resources/Scripts/Dymola/Fluid/ZoneEquipment/FanCoilUnit/Example/SingleZoneVariableFanConstantWaterFlowrate.mos"
      "Simulate and plot"),
    Documentation(info="<html>
      <p>
      This is an example model for the fan coil unit system model 
      demonstrating use-case with a variable fan, constant pump flowrate controller. 
      It consists of:
      <ul>
      <li>
      an instance of the fan coil unit system model <code>fanCoiUni</code>.
      </li>
      <li>
      thermal zone model <code>zon</code> of class <a href=\"modelica://Buildings.ThermalZones.EnergyPlus_9_6_0.ThermalZone\">
      Buildings.ThermalZones.EnergyPlus_9_6_0.ThermalZone</a>.
      </li>
      <li>
      ideal media sources <code>souCoo</code> and <code>souHea</code> for simulating 
      the supply of chilled water and heating hot-water respectively.
      </li>
      <li>
      fan coil unit controller <code>conVarFanConWat</code> of class <a href=\"modelica://Buildings.Fluid.ZoneEquipment.FanCoilUnit.Controls.VariableFanConstantWaterFlowrate\">
      Buildings.Fluid.ZoneEquipment.FanCoilUnit.Controls.VariableFanConstantWaterFlowrate</a>.
      </li>
      <li>
      occupancy schedule controller <code>occSch</code>.
      </li>
      <li>
      zone temperature setpoint controller <code>TZonSet</code>.
      </li>
      </ul>
      </p>
      <p>
      The simulation model provides a closed-loop example of <code>fanCoiUni</code> that
      is operated by <code>conVarFanConWat</code> and regulates the zone temperature 
      in <code>zon</code> at the setpoint generated by <code>TZonSet</code>.
      <br>
      The plots shopw the zone temperature regulation, controller outputs and the 
      fan coil unit response.
      </p>
      </html>", revisions="<html>
      <ul>
      <li>
      August 03, 2022 by Karthik Devaprasad:<br/>
      First implementation.
      </li>
      </ul>
      </html>"));
end SingleZoneVariableFanConstantWaterFlowrate;
