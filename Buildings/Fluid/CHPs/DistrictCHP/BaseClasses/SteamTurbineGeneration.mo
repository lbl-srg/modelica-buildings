within Buildings.Fluid.CHPs.DistrictCHP.BaseClasses;
block SteamTurbineGeneration
  "Steam turbine electrical generation"
  extends Modelica.Blocks.Icons.Block;

  parameter Real a[3] "Coefficients for exergy efficiency function";

  Buildings.Controls.OBC.CDL.Interfaces.RealInput TExh(
    displayUnit="degC",
    final unit="K",
    final quantity = "ThermodynamicTemperature")
    "Exhaust gas temperature inlet temperature"
    annotation (Placement(transformation(extent={{-140,20},{-100,60}})));

  Buildings.Controls.OBC.CDL.Interfaces.RealInput mExh_flow(
    final unit="kg/s",
    final quantity="MassFlowRate")
    "Exhaust mass flow rate"
    annotation (Placement(transformation(extent={{-140,-60},{-100,-20}})));

  Buildings.Controls.OBC.CDL.Interfaces.RealOutput PEle_ST(
    final quantity= "Power",
    final unit = "W")
    "Steam turbine generator power generation"
  annotation (Placement(transformation(extent={{100,-20},{140,20}})));

protected
  Modelica.Units.NonSI.Temperature_degF TExh_degF
    "Exhaust gas temperature in degree Fahrenheit";
  Real aExh
     "Exhaust gas specific exergy (J/kg)";

algorithm
  TExh_degF := (TExh-273.15)*(9/5) +32;
  aExh :=Functions.ExhaustSpecificExergy(TExh=TExh_degF)/0.0004299226
    "Convert the unit from Btu/lb to J/kg (1J/kg=0.000429923 Btu/Ib)";

  PEle_ST :=mExh_flow*aExh*Functions.ExhaustExergyEfficiency(a=a, TExh=
    TExh_degF)
    "Steam turbine electricity is obtained by the exhaust exergy times exergy efficiency";

annotation (defaultComponentName="steTurGen",
  Icon(graphics={Rectangle(
          extent={{-100,100},{100,-100}},
          fillColor={135,135,135},
          fillPattern=FillPattern.Solid,
          pattern=LinePattern.None),
        Line(points={{-58,73},{-58,-66}}, color={255,255,255}),
        Line(
          points={{-58,-12},{-22,-12},{2,20},{24,28},{50,30}},
          color={255,255,255},
          smooth=Smooth.Bezier),
        Line(points={{-72,-55},{65,-55}}, color={255,255,255}),
        Polygon(
          points={{-58,85},{-66,63},{-50,63},{-58,85}},
          lineColor={255,255,255},
          fillColor={255,255,255},
          fillPattern=FillPattern.Solid),
        Polygon(
          points={{71,-55},{49,-47},{49,-63},{71,-55}},
          lineColor={255,255,255},
          fillColor={255,255,255},
          fillPattern=FillPattern.Solid)}), Documentation(revisions="<html>
<ul>
<li>
May 28, 2024, by Zhanwei He:<br/>
First implementation.
</li>
</ul>
</html>", info="<html>
<p>
This block calculates the steam turbine power generation using the second law of
thermodynamics.
This subsystem (HRSG + steam turbine) can be considered a heat engine, transferring
exhaust heat from the gas turbine to generate usable electricity.
Thus the electricity generated by the steam turbine can be determined by multiplying
the exhaust exergy by the exergy efficiency.
Exergy, often referred to as available energy, is calculated by multiplying the
specific exergy by the exhaust mass flow rate.
</p>
<p align=\"center\">
<i>
P<sub>ste</sub> = a<sub>exh</sub> m<sub>exh</sub> &epsilon;<sub>exh</sub>,
</i>
</p>
<p>
where <i>P<sub>ste</sub></i> is the steam turbine power generation,
<i>a<sub>exh</sub> </i> is the specific exergy of exhaust gas from gas turbine,
which is obtained by using the function
<a href=\"modelica://Buildings.Fluid.CHPs.DistrictCHP.BaseClasses.Functions.ExhaustSpecificExergy\">
Buildings.Fluid.CHPs.DistrictCHP.BaseClasses.Functions.ExhaustSpecificExergy</a>,
<i>&epsilon;<sub>exh</sub></i> is the exergy efficiency, and the value is calculated
by using the function
<a href=\"modelica://Buildings.Fluid.CHPs.DistrictCHP.BaseClasses.Functions.ExhaustExergyEfficiency\">
Buildings.Fluid.CHPs.DistrictCHP.BaseClasses.Functions.ExhaustExergyEfficiency</a>.
</p>
<h4>References</h4>
<p>
Gülen, S. (2019). <i> Gas Turbine Combined Cycle Power Plants (1st ed.) </i>. CRC Press.
<a href=\"https://doi.org/10.1201/9780429244360\">[Link]</a>
</p>
<p>
Gülen, S. (2019) <i> Gas Turbines for Electric Power Generation.</i> Cambridge University Press.
<a href=\"https://doi.org/10.1017/9781108241625\">[Link]</a>
</p>
</html>"));
end SteamTurbineGeneration;
